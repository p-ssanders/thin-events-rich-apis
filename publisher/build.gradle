import org.springframework.cloud.contract.verifier.config.TestFramework

plugins {
    id 'maven-publish'
    id 'org.springframework.cloud.contract'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'com.h2database:h2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.apache.qpid:qpid-broker-core'
    testRuntimeOnly 'org.apache.qpid:qpid-broker-plugins-amqp-0-8-protocol'
    testRuntimeOnly 'org.apache.qpid:qpid-broker-plugins-memory-store'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

test {
    finalizedBy('publishToMavenLocal')
}

contractTest {
    useJUnitPlatform()
}

contracts {
    testFramework = TestFramework.JUNIT5
    packageWithBaseClasses = 'dev.samsanders.demo.rabbitmq.publisher.contracts'
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifact verifierStubsJar
        }
    }
}